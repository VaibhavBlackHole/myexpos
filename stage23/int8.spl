// fork system call, int 8

alias userSP R9;
userSP = SP;

alias oldPid R12;
oldPid = [SYSTEM_STATUS_TABLE + 1];

alias oldProcess R11;
oldProcess = PROCESS_TABLE + 16 * oldPid;

[oldProcess + 13] = userSP;
SP = [oldProcess + 11] * 512 - 1;

[oldProcess + 9] = 8;

// To get a new PID for the child process, invoke the Get Pcb Entry function from the process manager module.
multipush(R9, R11, R12);
R1 = 1;
call MOD_1;
multipop(R9, R11, R12);

if (R0 == -1) then
    [[PTBR + 2 * ((userSP - 1) / 512)] * 512 + (userSP - 1) % 512] = -1;
else 
    alias newPid R5;
    newPid = R0;
    alias newPTBR R8;
    newPTBR = PAGE_TABLE_BASE + 20 * newPid;
    alias newProcess R10;
    newProcess = PROCESS_TABLE + 16 * newPid;

    // STACK
    multipush(R5, R8, R9, R10, R11, R12);
    R1 = 1;
    call MOD_2;
    multipop(R5, R8, R9, R10, R11, R12);
    [newPTBR + 16] = R0;
    [newPTBR + 17] = "0110";
    multipush(R5, R8, R9, R10, R11, R12);
    R1 = 1;
    call MOD_2;
    multipop(R5, R8, R9, R10, R11, R12);
    [newPTBR + 18] = R0;
    [newPTBR + 19] = "0110";
   
 // USER AREA PAGE
    multipush(R5, R8, R9, R10, R11, R12);
    R1 = 1;
    call MOD_2;
    multipop(R5, R8, R9, R10, R11, R12);
    [newProcess + 11] = R0;

    // If the heap pages are not assigned for the parent process, allocate heap pages
    // by invoking the Get Free Page function of the memory manager
    if ([PTBR + 5] == "0000") then
        multipush(R5, R8, R9, R10, R11, R12);
        R1 = 1;
        call MOD_2;
        multipop(R5, R8, R9, R10, R11, R12);
        [PTBR + 4] = R0;
        [PTBR + 5] = "0110";
        [newPTBR + 4] = R0;
        [newPTBR + 5] = "0110";
        multipush(R5, R8, R9, R10, R11, R12);
        R1 = 1;
        call MOD_2;
        multipop(R5, R8, R9, R10, R11, R12);
        [PTBR + 6] = R0;
        [PTBR + 7] = "0110";
        [newPTBR + 6] = R0;
        [newPTBR + 7] = "0110";
    else 
        [newPTBR + 4] = [PTBR + 4];
        [newPTBR + 5] = "0110";
        [newPTBR + 6] = [PTBR + 6];
        [newPTBR + 7] = "0110";     
    endif;

    [MEMORY_FREE_LIST + [newPTBR + 4]] = [MEMORY_FREE_LIST + [newPTBR + 4]] + 1;
    [MEMORY_FREE_LIST + [newPTBR + 6]] = [MEMORY_FREE_LIST + [newPTBR + 6]] + 1;

    [newProcess + 1] = newPid;
    [newProcess + 3] = [oldProcess + 3];
    [newProcess + 6] = [oldProcess + 6];
    [newProcess + 10] = [oldProcess + 10];
    [newProcess + 7] = [oldProcess + 7];
    [newProcess + 13] = [oldProcess + 13];
    [newProcess + 9] = 0;
    [newProcess + 12] = 0;
    [newProcess + 0] = 0;
    [newProcess + 2] = [SYSTEM_STATUS_TABLE + 1];
    [newProcess + 4] = CREATED;


    alias iter R7;
    // Copy the per-process disk map table of the parent to the child.
    iter = 0;
    while (iter < 10) do
        [DISK_MAP_TABLE + newPid * 10 + iter] = [DISK_MAP_TABLE + oldPid * 10 + iter];
        iter = iter + 1;
    endwhile;

    //copying the per-process resource table from the parent to the child (the per-process resource table is
    //stored in the last 16 entries of the userAreaPage)
    alias curResTable R3;
    alias newResTable R4;
    alias counter R7;
    curResTable = ([oldProcess + 11] + 1) * 512 - 16;
    newResTable = ([newProcess + 11] + 1) * 512 - 16;

    counter = 0;
    while(counter < 16) do
        [newResTable + counter] = [curResTable + counter];
        [newResTable + counter + 1] = [curResTable + counter + 1];
        if([newResTable + counter] == SEMAPHORE) then
            alias semaphoreEntry R6;
            semaphoreEntry = SEMAPHORE_TABLE + 4 * [newResTable + counter + 1];
            [semaphoreEntry + 1] = [semaphoreEntry + 1] + 1;
        endif;
        counter = counter + 2;
    endwhile;
    
    // Initialize the page table of the child process.
    // As heap, code and library pages are shared by the parent process and the child process, copy these entries

    alias iter R7;

    // library
    iter = 0;
    while (iter < 4) do
        [newPTBR + iter] = [PTBR + iter];
        [newPTBR + iter + 1] = [PTBR + iter + 1];
        if ([newPTBR + iter] != -1) then
            [MEMORY_FREE_LIST + [newPTBR + iter]] = [MEMORY_FREE_LIST + [newPTBR + iter]] + 1;
        endif;
        iter = iter + 2;
    endwhile;

    // code pages
    iter = 8;
    while (iter < 16) do
        [newPTBR + iter] = [PTBR + iter];
        [newPTBR + iter + 1] = [PTBR + iter + 1];
        if ([newPTBR + iter] != -1) then
            [MEMORY_FREE_LIST + [newPTBR + iter]] = [MEMORY_FREE_LIST + [newPTBR + iter]] + 1;
        endif;
        iter = iter + 2;
    endwhile;

    // Copy content of the user stack pages of the parent to the user stack pages of the child word by word.
    iter = 0;
    while (iter < 512) do
        [[newPTBR + 16] * 512 + iter] = [[PTBR + 16] * 512 + iter];
        [[newPTBR + 18] * 512 + iter] = [[PTBR + 18] * 512 + iter];
        iter = iter + 1;
    endwhile;

    // Store the value in the BP register on top of the kernel stack of child process.
    [[newProcess + 11] * 512] = BP;
    // [newProcess + 12] = 1; // Not quite sure of this one.

    // Set up return values in the user stacks of the parent and the child processes.
    [[PTBR + 2 * ((userSP - 1) / 512)] * 512 + (userSP - 1) % 512] = newPid;
    [[newPTBR + 2 * ((userSP - 1) / 512)] * 512 + (userSP - 1) % 512] = 0;
    
endif;

[oldProcess + 9] = 0;
SP = [oldProcess + 13];

ireturn;
